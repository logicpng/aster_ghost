//---------------------------Anchors and OnTranslate--------------------------

//This .dic file is for setting up anchor words, which are links to set conversations attached to certain words automatically. It also deals with OnTranslate, which is a fairly unique function. All text goes through OnTranslate first before it displays on your ghost, which is how it knows to turn certain words into links.
//Why should I care about anchor words, you ask? Or what can I use them for? Well, you can set up anchor words to easy explanations for things your ghost might talk about that might confuse your user. People they know, places they've been, pop culture references the user might not get, information about you the creator or the world the characters from, items in an item menu, basically any number of things. Anchor words can be a handy repository of information! Or just an easy way to set up some cheap jokes. The power is yours!

//Make sure to read the walkthrough (http://www.ashido.com/ukagaka/walkthrough.html) for more details about how to properly format ghost dialogue!
//I will reiterate that you should be editing these in Notepad++, and in particular, you should set the Language to C to properly highlight all the text. It will make things A LOT EASIER for you, trust me! It will also help you keep track of your brackets in case you forget some. If you click on a line with a bracket in Notepad++, it should highlight where the other bracket ends, or turn purple if there's no finishing bracket (which you should definitely fix!)
//It will also highlight my commented lines in green, so you'll know where they are. Any line beginning with // is a commented line that the code will not read. It's just for your reference!

//This file is very short, so don't worry! And if you aren't planning on using anchor words, you just have to delete a few things and you'll be done!


//--OnTranslate

//As I said above, OnTranslate filters through all the text your ghost displays and does whatever you specify to it. That doesn't have to just be turning things into links, it could be anything. This is a powerful function that can be used in a lot of creative ways I bet, but I wouldn't mess with it too much since you can pretty easily break stuff if you don't know what you're doing.
//If you're not interested in any anchor words, delete all the lines that start with _talk = REPLACE under OnTranslate.

OnTranslate
	{
	_talk = reference0
 	_talk = REPLACE(_talk, "Options", "\_a[options]Options\_a")
 	_talk = REPLACE(_talk, "Utilities", "\_a[utilities]Utilities\_a")
 	_talk = REPLACE(_talk, "TerraByte", "\f[bold,1]\f[color,#275453]TerraByte\f[default]")
	//_talk = REPLACE(_talk, "Aster", "\f[color,#275453]Aster\f[default]")
	_talk = REPLACE(_talk, "Rigel", "\f[color,#C94948]Rigel\f[default]")
	_talk = REPLACE(_talk, "Altair", "\f[color,#C94948]Altair\f[default]")
	_talk = REPLACE(_talk, "Vega", "\f[color,#2D418E]Vega\f[default]")

	if noVibrate == 1 //if user disables rigel's visual vibration the related interval tags get eaten
	{
		_talk = REPLACE(_talk, "\i[60]", "")
		_talk = REPLACE(_talk, "\i[61]", "")
		_talk = REPLACE(_talk, "\i[62]", "")
	}
	
		if reference1 == "" && reference2 == "" { //send input box : no event (ref2) , no special flag (ref1)
            EVAL('"' + REPLACE(_talk,'"','" + CHR(0x22) + "') + '"')
        }
        else { //event result from ghost
            TOSTR(_talk)
        }
	}
	
	//Some brief explanation about what's going on in those REPLACE lines! Basically, the function looks through the script and looks for a defined word or set of words. In this case, it's "Anchor 1". It then replaces "Anchor 1" with the second set of text, "\_a[anchor1]Anchor 1\_a". \_a if you recall is the dialogue code for linking something, so this is linking the words Anchor 1 to something called anchor1 set up in the next function, OnAnchorSelect.
	//This is being used to change a word into a link, but you could conceivably use this to replace any text with any other kind of text if you wanted.

	
//--OnAnchorSelect
//OnAnchorSelect runs whenever someone clicks one of the links as specified above, or a link that begins with http://.

OnAnchorSelect
{
	if "http://" _in_ reference0 || "https://" _in_ reference0 //checking to see if the link is a web address
	{
	"\j[%(reference0)]\e" // \j is a tag that will open a specified url in the user's browser of choice. This basically just tells the ghost to open the webpage stored in reference0, which would be the address that the user just clicked. I didn't get too much into \j in the walkthrough page because I have literally never used it except here.
	}
	else //if it's NOT a web address
	{
	case reference0 //case is a unique thing that only really shows up in anchor.dic, and it basically sets up reference0 for evaluation to see if matches any of the below criteria, which are all tagged with when. You can read more about this on the coding page on the walkthrough site. You can add as many things here as you want to set up above as long as you follow the basic format.
		{
			when "options"; TalkConfig //if the link is this value
			when "utilities"; UtilityMenu //if the link is this value
			//a link from vega's random dialogue, stage 2
			when "system32"; "\![open,explorer,C:/Windows/System32]\w8\w8\w8\0\s[21]\i[12]Do you see?\w8\w8 There's so much...\w8\w8\n[150]\0\s[20]\i[7]Bad structure,\w8 too many files.\w8\w8\0\s[23] I want to organize it.\w8\w8\n[150]\0\s[20]\i[10]\i[8]But,\w8 then it'll break.\w8\w8 \0\s[20]\i[10]\i[7]Annoying..."
		}
	}
}

//This bit of code is what makes menu options that don't start with On direct to Select.options instead. Remove or comment it if you prefer!
OnChoiceSelect
{
	EVAL("Select.%(reference0)")
}